pipeline {
    agent any

    tools {
        maven 'M3'
    }

    parameters {
        string(name: 'BRANCH', defaultValue: 'master', description: 'Branch a construir')
    }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-authors'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
    }

    stages {
        stage('Checkout') {
            steps { 
                sh 'git config --global --add safe.directory "*"'
                checkout scm 
            }
        }

        stage('Build & Unit Tests') {
            steps {
                dir('lms-authors') { sh 'mvn clean test' }
            }
        }

        stage('Static Analysis & Mutation') {
            parallel {
                stage('SpotBugs') {
                    steps { dir('lms-authors') { sh 'mvn com.github.spotbugs:spotbugs-maven-plugin:4.8.6.2:spotbugs' } }
                }
                stage('Pitest') {
                    steps { dir('lms-authors') { sh 'mvn org.pitest:pitest-maven:mutationCoverage' } }
                }
            }
        }

        stage('Package Artifact') {
            steps {
                dir('lms-authors') {
                    sh 'mvn package -DskipTests'
                    archiveArtifacts artifacts: 'target/*.jar'
                }
            }
        }

        stage('Build & Push Docker Image') {
            steps {
                dir('lms-authors') {
                    script {
                        def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"

                        docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                            def customImage = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            customImage.push("${env.BUILD_TAG}")
                            customImage.push("latest")
                        }
                    }
                }
            }
        }

        stage('Blue-Green Deploy') {
            steps {
                dir('lms-authors') {
                    script {
                        def isGreenActive = sh(script: "docker ps --format '{{.Names}}' | grep authors-green || true", returnStdout: true).trim()
                        env.ACTIVE_COLOR = isGreenActive ? "green" : "blue"
                        env.NEXT_COLOR = isGreenActive ? "blue" : "green"
                        
                        // Portas para Authors
                        env.NEXT_PORT = (env.NEXT_COLOR == "blue") ? "30088" : "30089"
                        // Portas MongoDB: 27020 (Blue) e 27021 (Green)
                        env.NEXT_MONGO_PORT = (env.NEXT_COLOR == "blue") ? "27020" : "27021"

                        echo "Ativo: ${env.ACTIVE_COLOR} | Deploying Authors: ${env.NEXT_COLOR} na porta ${env.NEXT_PORT}"

                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "MONGO_PORT=${env.NEXT_MONGO_PORT}"
                        ]) {
                            sh "docker network create lms_network || true"
                            sh "docker compose -p authors-${env.NEXT_COLOR} down -v || true"
                            sh "docker compose -p authors-${env.NEXT_COLOR} up -d"

                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def containerId = sh(script: "docker compose -p authors-${env.NEXT_COLOR} ps -q authors", returnStdout: true).trim()
                                    if (!containerId) return false
                                    def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' ${containerId}", returnStdout: true).trim()
                                    return (health == "healthy")
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Verify Strategies (k6)') {
            steps {
                dir('lms-authors') {
                    script {
                        def containerName = sh(script: "docker ps --filter 'name=authors-${env.NEXT_COLOR}' --format '{{.Names}}' | head -n 1", returnStdout: true).trim()
                        
                        withEnv([
                            "KILLSWITCH_ACTIVE=true",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "MONGO_PORT=${env.NEXT_MONGO_PORT}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"
                        ]) {
                            sh "docker compose -p authors-${env.NEXT_COLOR} up -d --no-deps authors"
                            sleep 10
                            sh "docker run --rm --network lms_network -i loadimpact/k6 run - < verify_strategies.js -e TARGET_URL='http://${containerName}:8088'"
                        }

                        withEnv([
                            "KILLSWITCH_ACTIVE=false",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "MONGO_PORT=${env.NEXT_MONGO_PORT}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"
                        ]) {
                            sh "docker compose -p authors-${env.NEXT_COLOR} up -d --no-deps authors"
                        }
                    }
                }
            }
        }

        stage('Manual Approval') {
            steps {
                input message: "Authors ${env.NEXT_COLOR} validado. Promover?", ok: "Promover"
            }
        }

        stage('Smoke Tests') {
            steps {
                dir('lms-authors') {
                    script {
                        sh "docker run --rm --network host -i loadimpact/k6 run - < smoke_test.js -e TARGET_URL='http://localhost:${env.NEXT_PORT}/api/authors'"
                    }
                }
            }
        }

        stage('Cleanup Old Version') {
            steps {
                dir('lms-authors') {
                    sh "docker compose -p authors-${env.ACTIVE_COLOR} down || true"
                }
            }
        }
    }

    post {
        failure {
            script {
                dir('lms-authors') { sh "docker compose -p authors-${env.NEXT_COLOR} down || true" }
            }
        }
        always { cleanWs() }
    }
}