pipeline {
    agent any

    tools {
        maven 'M3'
    }

    parameters {
        string(name: 'BRANCH', defaultValue: 'main', description: 'Branch a construir')
    }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-genre'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
    }

    stages {
        stage('Checkout') {
            steps { checkout scm }
        }

        stage('Build & Unit Tests') {
            steps {
                dir('lms-genre') { sh 'mvn clean test' }
            }
        }

        stage('Static Analysis & Mutation') {
            parallel {
                stage('SpotBugs') {
                    steps { dir('lms-genre') { sh 'mvn com.github.spotbugs:spotbugs-maven-plugin:4.8.6.2:spotbugs' } }
                }
                stage('Pitest') {
                    steps { dir('lms-genre') { sh 'mvn org.pitest:pitest-maven:mutationCoverage' } }
                }
            }
        }

        stage('Package Artifact') {
            steps {
                dir('lms-genre') {
                    sh 'mvn package -DskipTests'
                    archiveArtifacts artifacts: 'target/*.jar'
                }
            }
        }

        stage('Build & Push Docker Image') {
            steps {
                dir('lms-genre') {
                    script {
                        def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"

                        docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                            def customImage = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            customImage.push("${env.BUILD_TAG}")
                            customImage.push("latest")
                        }
                    }
                }
            }
        }

        stage('Blue-Green Deploy') {
            steps {
                dir('lms-genre') {
                    script {
                        def isGreenActive = sh(script: "docker ps --format '{{.Names}}' | grep genre-green || true", returnStdout: true).trim()
                        env.ACTIVE_COLOR = isGreenActive ? "green" : "blue"
                        env.NEXT_COLOR = isGreenActive ? "blue" : "green"
                        env.NEXT_PORT = (env.NEXT_COLOR == "blue") ? "30070" : "30071"

                        echo "Ativo: ${env.ACTIVE_COLOR} | Deploying Genre: ${env.NEXT_COLOR} na porta ${env.NEXT_PORT}"

                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=0"
                        ]) {
                            sh "docker network create lms_network || true"

                            // Sobe a nova cor (genre-service e mongo_genres)
                            sh "docker compose -p genre-${env.NEXT_COLOR} up -d"

                            echo "--- Health Check da nova versão (${env.NEXT_COLOR}) ---"
                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def containerId = sh(script: "docker compose -p genre-${env.NEXT_COLOR} ps -q genre-service", returnStdout: true).trim()
                                    if (!containerId) return false
                                    def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' ${containerId}", returnStdout: true).trim()
                                    return (health == "healthy")
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Verify Strategies (k6)') {
            steps {
                dir('lms-genre') {
                    script {
                        def containerName = sh(script: "docker ps --filter 'name=genre-${env.NEXT_COLOR}-genre-service' --format '{{.Names}}' | head -n 1", returnStdout: true).trim()
                        echo "--- Validando Estratégias no container: ${containerName} ---"

                        withEnv([
                            "KILLSWITCH_ACTIVE=true",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "WEIGHT=0"
                        ]) {
                            sh "docker compose -p genre-${env.NEXT_COLOR} up -d --no-deps genre-service"
                            sleep 10
                            sh """
                                docker run --rm --network lms_network -i loadimpact/k6 run - < verify_strategies.js \
                                -e TARGET_URL="http://${containerName}:8089" \
                                -e KILLSWITCH_ACTIVE=true
                            """
                        }

                        echo "--- Restaurando Serviço (Kill Switch OFF) ---"
                        withEnv([
                            "KILLSWITCH_ACTIVE=false",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "WEIGHT=0"
                        ]) {
                            sh "docker compose -p genre-${env.NEXT_COLOR} up -d --no-deps genre-service"
                        }
                    }
                }
            }
        }

        stage('Manual Approval') {
            steps {
                input message: "Genre ${env.NEXT_COLOR} validado. Deseja promover para 100% e desligar ${env.ACTIVE_COLOR}?", ok: "Promover"
            }
        }

        stage('Smoke Tests') {
            steps {
                dir('lms-genre') {
                    script {
                        echo "--- Smoke Tests na porta ${env.NEXT_PORT} ---"
                        sh """
                            docker run --rm --network host -i loadimpact/k6 run - < smoke_test.js \
                            -e TARGET_URL="http://localhost:${env.NEXT_PORT}/api/genres"
                        """
                    }
                }
            }
        }

        stage('Switch Traffic & Cleanup') {
            steps {
                script {
                    echo "--- Sucesso! Removendo versão antiga: ${env.ACTIVE_COLOR} ---"
                    dir('lms-genre') {
                        // Promover o tráfego no Traefik (ajustando o peso se necessário via compose)
                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=100"
                        ]) {
                            sh "docker compose -p genre-${env.NEXT_COLOR} up -d --no-deps genre-service"
                        }
                        sh "docker compose -p genre-${env.ACTIVE_COLOR} down || true"
                    }
                }
            }
        }
    }

    post {
        failure {
            script {
                echo "--- Falha no Deploy. Limpando tentativa de ${env.NEXT_COLOR} ---"
                dir('lms-genre') {
                    sh "docker compose -p genre-${env.NEXT_COLOR} down || true"
                }
            }
        }
        always { cleanWs() }
    }
}