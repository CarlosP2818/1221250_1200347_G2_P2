pipeline {
    agent any
    tools { maven 'M3' }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-genre'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
    }

    stages {
        stage('Checkout & Build') {
            steps {
                checkout scm
                sh "mvn clean package -DskipTests"
            }
        }

        stage('Build & Push Image') {
            steps {
                script {
                    def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                    env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"
                    docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                        def customImage = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                        customImage.push("${env.BUILD_TAG}")
                        customImage.push("latest")
                    }
                }
            }
        }

        stage('Blue-Green Deploy') {
            steps {
                script {
                    def isGreenActive = sh(script: "docker ps --format '{{.Names}}' | grep genre-green || true", returnStdout: true).trim()
                    env.ACTIVE_COLOR = isGreenActive ? "green" : "blue"
                    env.NEXT_COLOR = isGreenActive ? "blue" : "green"
                    env.NEXT_PORT = (env.NEXT_COLOR == "blue") ? "30070" : "30071"

                    withEnv([
                        "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                        "COLOR=${env.NEXT_COLOR}",
                        "HOST_PORT=${env.NEXT_PORT}",
                        "WEIGHT=0"
                    ]) {
                        sh "docker network create lms_network || true"
                        // Cleanup de tentativas falhadas e sobe a nova cor
                        sh "docker compose -p genre-${env.NEXT_COLOR} down -v || true"
                        sh "docker compose -p genre-${env.NEXT_COLOR} up -d"

                        timeout(time: 5, unit: 'MINUTES') {
                            waitUntil {
                                def containerId = sh(script: "docker compose -p genre-${env.NEXT_COLOR} ps -q genre-service", returnStdout: true).trim()
                                if (!containerId) return false
                                def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' ${containerId}", returnStdout: true).trim()
                                return (health == "healthy")
                            }
                        }
                    }
                }
            }
        }

        stage('Verify Strategies (k6)') {
            steps {
                script {
                    def containerName = sh(script: "docker ps --filter 'name=genre-${env.NEXT_COLOR}' --format '{{.Names}}' | head -n 1", returnStdout: true).trim()
                    withEnv(["KILLSWITCH_ACTIVE=true", "COLOR=${env.NEXT_COLOR}", "HOST_PORT=${env.NEXT_PORT}", "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"]) {
                        sh "docker compose -p genre-${env.NEXT_COLOR} up -d --no-deps genre-service"
                        sleep 10
                        sh "docker run --rm --network lms_network -i loadimpact/k6 run - < verify_strategies.js -e TARGET_URL='http://${containerName}:8089' -e KILLSWITCH_ACTIVE=true"
                    }
                    // Restaurar para OFF
                    withEnv(["KILLSWITCH_ACTIVE=false", "COLOR=${env.NEXT_COLOR}", "HOST_PORT=${env.NEXT_PORT}", "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"]) {
                        sh "docker compose -p genre-${env.NEXT_COLOR} up -d --no-deps genre-service"
                    }
                }
            }
        }

        stage('Manual Approval & Switch') {
            steps {
                input message: "Promover Genre ${env.NEXT_COLOR} para 100%?"
                script {
                    withEnv(["DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}", "COLOR=${env.NEXT_COLOR}", "HOST_PORT=${env.NEXT_PORT}", "WEIGHT=100"]) {
                        sh "docker compose -p genre-${env.NEXT_COLOR} up -d --no-deps genre-service"
                    }
                }
            }
        }

        stage('Cleanup Old Version') {
            steps {
                sh "docker compose -p genre-${env.ACTIVE_COLOR} down || true"
            }
        }
    }

    post {
        failure {
            script { sh "docker compose -p genre-${env.NEXT_COLOR} down || true" }
        }
        always { cleanWs() }
    }
}