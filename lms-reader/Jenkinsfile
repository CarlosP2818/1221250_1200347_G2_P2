pipeline {
    agent any

    tools {
        maven 'M3'
    }

    parameters {
        string(name: 'BRANCH', defaultValue: 'main', description: 'Branch a construir')
    }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-readers'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
    }

    stages {
        stage('Checkout') {
            steps { checkout scm }
        }

        stage('Build & Unit Tests') {
            steps {
                dir('lms-reader') { sh 'mvn clean test' }
            }
        }

        stage('Static Analysis & Mutation') {
            parallel {
                stage('SpotBugs') {
                    steps { dir('lms-reader') { sh 'mvn com.github.spotbugs:spotbugs-maven-plugin:4.8.6.2:spotbugs' } }
                }
                stage('Pitest') {
                    steps { dir('lms-reader') { sh 'mvn org.pitest:pitest-maven:mutationCoverage' } }
                }
            }
        }

        stage('Package Artifact') {
            steps {
                dir('lms-reader') {
                    sh 'mvn package -DskipTests'
                    archiveArtifacts artifacts: 'target/*.jar'
                }
            }
        }

        stage('Build & Push Docker Image') {
            steps {
                dir('lms-reader') {
                    script {
                        def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"

                        docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                            def customImage = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            customImage.push("${env.BUILD_TAG}")
                            customImage.push("latest")
                        }
                    }
                }
            }
        }

        stage('Blue-Green Deploy') {
            steps {
                dir('lms-reader') {
                    script {
                        def isGreenActive = sh(script: "docker ps --format '{{.Names}}' | grep readers-green || true", returnStdout: true).trim()
                        env.ACTIVE_COLOR = isGreenActive ? "green" : "blue"
                        env.NEXT_COLOR = isGreenActive ? "blue" : "green"
                        env.NEXT_PORT = (env.NEXT_COLOR == "blue") ? "30090" : "30091"

                        echo "Ativo: ${env.ACTIVE_COLOR} | Deploying Readers: ${env.NEXT_COLOR} na porta ${env.NEXT_PORT}"

                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=0" // Entra com peso 0 no Traefik inicialmente
                        ]) {
                            sh "docker network create lms_network || true"
                            sh "docker compose -p readers-${env.NEXT_COLOR} up -d readers"

                            echo "--- Health Check da nova versão (${env.NEXT_COLOR}) ---"
                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def containerId = sh(script: "docker compose -p readers-${env.NEXT_COLOR} ps -q readers", returnStdout: true).trim()
                                    if (!containerId) return false
                                    def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' ${containerId}", returnStdout: true).trim()
                                    return (health == "healthy")
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Verify Strategies (k6)') {
            steps {
                dir('lms-reader') {
                    script {
                        def containerName = sh(script: "docker ps --filter 'name=readers-${env.NEXT_COLOR}' --format '{{.Names}}' | head -n 1", returnStdout: true).trim()
                        echo "--- Validando Estratégias Readers no container: ${containerName} ---"

                        withEnv([
                            "KILLSWITCH_ACTIVE=true",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "WEIGHT=0"
                        ]) {
                            sh "docker compose -p readers-${env.NEXT_COLOR} up -d --no-deps readers"
                            sleep 10
                            // Porta 8090 interna do Readers
                            sh "docker run --rm --network lms_network -i loadimpact/k6 run - < verify_strategies.js -e TARGET_URL='http://${containerName}:8090'"
                        }

                        echo "--- Restaurando Kill Switch OFF ---"
                        withEnv([
                            "KILLSWITCH_ACTIVE=false",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "WEIGHT=0"
                        ]) {
                            sh "docker compose -p readers-${env.NEXT_COLOR} up -d --no-deps readers"
                        }
                    }
                }
            }
        }

        stage('Manual Approval & Switch') {
            steps {
                input message: "Readers ${env.NEXT_COLOR} validado. Deseja promover para 100%?", ok: "Promover"
                script {
                    dir('lms-reader') {
                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=100"
                        ]) {
                            sh "docker compose -p readers-${env.NEXT_COLOR} up -d --no-deps readers"
                        }
                    }
                }
            }
        }

        stage('Smoke Tests') {
            steps {
                dir('lms-reader') {
                    script {
                        echo "--- Smoke Tests na porta ${env.NEXT_PORT} ---"
                        sh "docker run --rm --network host -i loadimpact/k6 run - < smoke_test.js -e READERS_URL='http://localhost:${env.NEXT_PORT}/api/readers'"
                    }
                }
            }
        }

        stage('Cleanup Old Version') {
            steps {
                script {
                    echo "--- Removendo versão antiga: ${env.ACTIVE_COLOR} ---"
                    dir('lms-reader') {
                        sh "docker compose -p readers-${env.ACTIVE_COLOR} down || true"
                    }
                }
            }
        }
    }

    post {
        failure {
            script {
                dir('lms-reader') { sh "docker compose -p readers-${env.NEXT_COLOR} down || true" }
            }
        }
        always { cleanWs() }
    }
}