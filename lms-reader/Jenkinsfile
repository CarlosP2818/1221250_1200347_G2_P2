pipeline {
    agent any
    tools { maven 'M3' }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-readers'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
    }

    stages {
        stage('Checkout & Build') {
            steps {
                checkout scm
                dir('lms-reader') { sh 'mvn clean package -DskipTests' }
            }
        }

        stage('Build & Push Docker') {
            steps {
                dir('lms-reader') {
                    script {
                        def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"

                        docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                            def customImage = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            customImage.push("${env.BUILD_TAG}")
                            customImage.push("latest")
                        }
                    }
                }
            }
        }

        stage('Blue-Green Deploy') {
            steps {
                dir('lms-reader') {
                    script {
                        // 1. Identificar cor ativa (procura pelo sufixo no nome do container)
                        def activeColor = sh(script: "docker ps --format '{{.Names}}' | grep users-green && echo 'green' || echo 'blue'", returnStdout: true).trim()
                        def nextColor = (activeColor == "blue") ? "green" : "blue"
                        def nextPort = (nextColor == "blue") ? "8080" : "8081"

                        echo "--- Deploying ${nextColor.toUpperCase()} version ---"

                        withEnv([
                            "COLOR=${nextColor}",
                            "HOST_PORT=${nextPort}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"
                        ]) {
                            // Sobe o novo ambiente com um nome de projeto diferente para isolar
                            sh "docker compose -p users-${nextColor} up -d users"

                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def containerId = sh(script: "docker compose -p users-${nextColor} ps -q users", returnStdout: true).trim()
                                    def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' ${containerId}", returnStdout: true).trim()
                                    echo "Health de ${nextColor}: ${health}"
                                    return (health == "healthy")
                                }
                            }

                            // Smoke Tests contra a nova cor
                            sh "docker run --rm --network host loadimpact/k6 run - < smoke_test.js -e READERS_URL='http://localhost:${nextPort}/api/readers'"

                            // Se chegou aqui, removemos o antigo
                            echo "--- Desligando versÃ£o antiga (${activeColor}) ---"
                            sh "docker compose -p users-${activeColor} down || true"
                        }
                    }
                }
            }
        }
    }
}