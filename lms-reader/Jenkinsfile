pipeline {
    agent any

    tools { maven 'M3' }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-readers'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
    }

    stages {
        stage('Checkout & Build') {
            steps {
                checkout scm
                dir('lms-reader') { sh 'mvn clean test package -DskipTests' }
            }
        }

        stage('Static Analysis & Mutation') {
            parallel {
                stage('SpotBugs') {
                    steps { dir('lms-reader') { sh 'mvn com.github.spotbugs:spotbugs-maven-plugin:4.8.6.2:spotbugs' } }
                }
                stage('Pitest') {
                    steps { dir('lms-reader') { sh 'mvn org.pitest:pitest-maven:mutationCoverage' } }
                }
            }
        }

        stage('Build & Push Docker Image') {
            steps {
                dir('lms-reader') {
                    script {
                        def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"
                        docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                            def customImage = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            customImage.push("${env.BUILD_TAG}")
                        }
                    }
                }
            }
        }

        stage('Canary Deploy (Weight 0)') {
            steps {
                dir('lms-reader') {
                    script {
                        // Detectar se o Green está ativo para decidir a próxima cor
                        def isGreenActive = sh(script: "docker ps --format '{{.Names}}' | grep readers-green || true", returnStdout: true).trim()
                        env.ACTIVE_COLOR = isGreenActive ? "green" : "blue"
                        env.NEXT_COLOR = isGreenActive ? "blue" : "green"
                        env.NEXT_PORT = (env.NEXT_COLOR == "blue") ? "30090" : "30091"

                        echo "Ativo: ${env.ACTIVE_COLOR} | Deploying Canary: ${env.NEXT_COLOR} (Peso 0)"

                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=0"
                        ]) {
                            sh "docker network create lms_network || true"
                            sh "docker compose -p readers-${env.NEXT_COLOR} up -d"

                            echo "--- Health Check da nova versão (${env.NEXT_COLOR}) ---"
                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' readers-${env.NEXT_COLOR}", returnStdout: true).trim()
                                    return (health == "healthy")
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Verify Canary (k6)') {
            steps {
                dir('lms-reader') {
                    script {
                        def containerName = "readers-${env.NEXT_COLOR}"
                        echo "--- Validando Canary no container: ${containerName} ---"
                        // k6 testa diretamente a nova cor na rede interna
                        sh "docker run --rm --network lms_network -i loadimpact/k6 run - < smoke_test.js -e READERS_URL='http://${containerName}:8090/api/readers'"
                    }
                }
            }
        }

        stage('Manual Approval & Switch') {
            steps {
                input message: "Canary ${env.NEXT_COLOR} validado. Deseja promover para 100% do tráfego?", ok: "Promover"

                script {
                    dir('lms-reader') {
                        echo "--- Promovendo ${env.NEXT_COLOR} para Peso 100 ---"
                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=100"
                        ]) {
                            sh "docker compose -p readers-${env.NEXT_COLOR} up -d --no-deps readers"
                        }
                    }
                }
            }
        }

        stage('Final Smoke Tests (Traefik)') {
            steps {
                dir('lms-reader') {
                    script {
                        echo "--- Smoke Tests via Traefik (Porta 80) ---"
                        sh "docker run --rm --network host -i loadimpact/k6 run - < smoke_test.js -e READERS_URL='http://localhost/api/readers'"
                    }
                }
            }
        }

        stage('Cleanup Old Version & Promote Latest') {
            steps {
                script {
                    echo "--- Sucesso! Removendo versão antiga: ${env.ACTIVE_COLOR} ---"
                    dir('lms-reader') {
                        sh "docker compose -p readers-${env.ACTIVE_COLOR} down || true"
                    }

                    docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                        sh "docker pull ${IMAGE_FULL_NAME}:${env.BUILD_TAG}"
                        sh "docker tag ${IMAGE_FULL_NAME}:${env.BUILD_TAG} ${IMAGE_FULL_NAME}:latest"
                        sh "docker push ${IMAGE_FULL_NAME}:latest"
                    }
                }
            }
        }
    }

    post {
        failure {
            script {
                echo "--- Falha no Canary. Removendo tentativa de ${env.NEXT_COLOR} ---"
                dir('lms-reader') {
                    sh "docker compose -p readers-${env.NEXT_COLOR} down || true"
                }
            }
        }
        always { cleanWs() }
    }
}