pipeline {
    agent any

    tools {
        maven 'M3'
    }

    parameters {
        string(name: 'BRANCH', defaultValue: 'main', description: 'Branch a construir')
    }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-authnusers'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
    }

    stages {

        stage('Checkout') {
            steps { checkout scm }
        }

        stage('Build & Unit Tests') {
            steps {
                dir('lms-authnusers') {
                    sh 'mvn clean test'
                }
            }
        }

        stage('Static Analysis & Mutation') {
            parallel {
                stage('SpotBugs') {
                    steps {
                        dir('lms-authnusers') {
                            sh 'mvn com.github.spotbugs:spotbugs-maven-plugin:4.8.6.2:spotbugs'
                        }
                    }
                }
                stage('Pitest') {
                    steps {
                        dir('lms-authnusers') {
                            sh 'mvn org.pitest:pitest-maven:mutationCoverage'
                        }
                    }
                }
            }
        }

        stage('Package Artifact') {
            steps {
                dir('lms-authnusers') {
                    sh 'mvn package -DskipTests'
                    archiveArtifacts artifacts: 'target/*.jar'
                }
            }
        }

        stage('Build & Push Docker Image') {
            steps {
                dir('lms-authnusers') {
                    script {
                        def shortCommit = sh(
                            script: "git rev-parse --short HEAD",
                            returnStdout: true
                        ).trim()

                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"

                        docker.withRegistry(
                            'https://index.docker.io/v1/',
                            DOCKER_REGISTRY_CREDENTIALS
                        ) {
                            def image = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            image.push("${env.BUILD_TAG}")
                            image.push("latest")
                        }
                    }
                }
            }
        }

        stage('Blue-Green Deploy') {
            steps {
                dir('lms-authnusers') {
                    script {

                        def isGreenUp = sh(
                            script: "docker compose -p users-green ps -q users || true",
                            returnStdout: true
                        ).trim()

                        env.ACTIVE_COLOR = isGreenUp ? "green" : "blue"
                        env.NEXT_COLOR   = isGreenUp ? "blue"  : "green"

                        echo "Ativo: ${env.ACTIVE_COLOR} | Deploying: ${env.NEXT_COLOR}"

                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}"
                        ]) {
                            sh "docker network create lms_network || true"
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d users"

                            echo "--- Health Check (${env.NEXT_COLOR}) ---"
                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def cid = sh(
                                        script: "docker compose -p users-${env.NEXT_COLOR} ps -q users",
                                        returnStdout: true
                                    ).trim()

                                    if (!cid) return false

                                    def health = sh(
                                        script: "docker inspect --format='{{.State.Health.Status}}' ${cid}",
                                        returnStdout: true
                                    ).trim()

                                    return health == "healthy"
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Verify Strategies (k6)') {
            steps {
                dir('lms-authnusers') {
                    script {
                        def containerName = sh(
                            script: "docker ps --filter 'name=users-${env.NEXT_COLOR}' --format '{{.Names}}' | head -n 1",
                            returnStdout: true
                        ).trim()

                        echo "--- Validando estratégias no container ${containerName} ---"

                        withEnv([
                            "KILLSWITCH_ACTIVE=true",
                            "COLOR=${env.NEXT_COLOR}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"
                        ]) {
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d --no-deps users"
                            sleep 10

                            sh """
                                docker run --rm --network lms_network -i loadimpact/k6 run - < verify_strategies.js \
                                -e TARGET_URL="http://${containerName}:8080"
                            """
                        }

                        withEnv([
                            "KILLSWITCH_ACTIVE=false",
                            "COLOR=${env.NEXT_COLOR}",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"
                        ]) {
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d --no-deps users"
                        }
                    }
                }
            }
        }

        stage('Manual Approval') {
            steps {
                input message: "Versão ${env.NEXT_COLOR} validada. Deseja promover tráfego?",
                      ok: "Promover"
            }
        }

        stage('Smoke Tests') {
            when {
                expression { params.BRANCH == 'dev' || params.BRANCH == 'staging' }
            }
            steps {
                dir('lms-authnusers') {
                    sh """
                        docker run --rm --network lms_network -i loadimpact/k6 run - < smoke_test.js \
                        -e AUTH_URL="http://traefik/api/users/login"
                    """
                }
            }
        }

        stage('Switch Traffic') {
            steps {
                script {
                    echo "--- Switching traffic to ${env.NEXT_COLOR} ---"
                    sh "docker kill -s HUP traefik || docker restart traefik"
                }
            }
        }
    }

    post {
        failure {
            script {
                echo "--- Falha no deploy da ${env.NEXT_COLOR} ---"
            }
        }
        always {
            cleanWs()
        }
    }
}
