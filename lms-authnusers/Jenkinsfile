pipeline {
    agent any

    tools {
        maven 'M3'
    }

    parameters {
        string(name: 'BRANCH', defaultValue: 'main', description: 'Branch a construir')
    }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-authnusers'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
    }

    stages {
        stage('Checkout') {
            steps { checkout scm }
        }

        stage('Build & Unit Tests') {
            steps {
                dir('lms-authnusers') { sh 'mvn clean test' }
            }
        }

        stage('Static Analysis & Mutation') {
            parallel {
                stage('SpotBugs') {
                    steps { dir('lms-authnusers') { sh 'mvn com.github.spotbugs:spotbugs-maven-plugin:4.8.6.2:spotbugs' } }
                }
                stage('Pitest') {
                    steps { dir('lms-authnusers') { sh 'mvn org.pitest:pitest-maven:mutationCoverage' } }
                }
            }
        }

        stage('Package Artifact') {
            steps {
                dir('lms-authnusers') {
                    sh 'mvn package -DskipTests'
                    archiveArtifacts artifacts: 'target/*.jar'
                }
            }
        }

        stage('Build & Push Docker Image') {
            steps {
                dir('lms-authnusers') {
                    script {
                        def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"

                        docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                            def customImage = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            customImage.push("${env.BUILD_TAG}")
                            customImage.push("latest")
                        }
                    }
                }
            }
        }

        stage('Blue-Green Deploy') {
            steps {
                dir('lms-authnusers') {
                    script {

                        def isGreenActive = sh(script: "docker ps --format '{{.Names}}' | grep users-green || true", returnStdout: true).trim()
                        env.ACTIVE_COLOR = isGreenActive ? "green" : "blue"
                        env.NEXT_COLOR = isGreenActive ? "blue" : "green"
                        env.NEXT_PORT = (env.NEXT_COLOR == "blue") ? "30080" : "30081"

                        echo "Ativo: ${env.ACTIVE_COLOR} | Deploying: ${env.NEXT_COLOR} na porta ${env.NEXT_PORT}"

                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}"
                        ]) {
                            sh "docker network create lms_network || true"

                            // Sobe a nova cor com um nome de projeto isolado
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d users"

                            echo "--- Health Check da nova versão (${env.NEXT_COLOR}) ---"
                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def containerId = sh(script: "docker compose -p users-${env.NEXT_COLOR} ps -q users", returnStdout: true).trim()
                                    if (!containerId) return false
                                    def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' ${containerId}", returnStdout: true).trim()
                                    return (health == "healthy")
                                }
                            }
                        }
                    }
                }
            }
        }

       stage('Verify Strategies with Auth') {
           steps {
               script {
                   echo "--- Realizando Login para obter Token na porta ${env.NEXT_PORT} ---"

                   // 2. Usamos aspas simples no JSON para evitar problemas com o Shell
                   def token = sh(script: """
                       curl -s -i -X POST http://localhost:${env.NEXT_PORT}/api/public/login \
                       -H "Content-Type: application/json" \
                       -d '{"username": "maria@gmail.com", "password": "Mariaroberta!123"}' | \
                       grep -i '^Authorization:' | sed 's/[Aa]uthorization: //' | tr -d '\\r\\n '
                   """, returnStdout: true).trim()

                   if (!token || token.length() < 10) {
                       error "Falha ao obter o token. O login falhou ou o header Authorization está vazio."
                   }

                   echo "Token obtido com sucesso! (Prefixo: ${token.take(15)}...)"

                   echo "--- Testando Dark Launch (Header Secreto) ---"
                   sh """
                       curl -s -i -H "Authorization: Bearer ${token}" \
                            -H "X-LMS-Beta-Access: true" \
                            http://localhost:${env.NEXT_PORT}/api/admin/users/api/readers/new-feature | grep "200"
                   """

                   echo "--- Testando Kill Switch (Evidence of Toggling) ---"
                   withEnv(["KILLSWITCH_ACTIVE=true", "COLOR=${env.NEXT_COLOR}", "HOST_PORT=${env.NEXT_PORT}", "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"]) {
                       sh "docker compose -p users-${env.NEXT_COLOR} up -d --no-deps users"
                       echo "Aguardando 10s para o Kill Switch ativar..."
                       sleep 10

                       sh """
                           curl -s -i -H "Authorization: Bearer ${token}" \
                                http://localhost:${env.NEXT_PORT}/api/admin/users | grep "503"
                       """
                   }

                   // Restaurar para que o pipeline termine com o serviço funcional
                   withEnv(["KILLSWITCH_ACTIVE=false", "COLOR=${env.NEXT_COLOR}", "HOST_PORT=${env.NEXT_PORT}", "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"]) {
                       sh "docker compose -p users-${env.NEXT_COLOR} up -d --no-deps users"
                   }
               }
           }
       }

               stage('Manual Approval') {
                   steps {
                       input message: "Versão ${env.NEXT_COLOR} validada. Deseja promover e desligar ${env.ACTIVE_COLOR}?", ok: "Promover"
                   }
               }

        stage('Smoke Tests') {
            steps {
                dir('lms-authnusers') {
                    script {
                        echo "--- Smoke Tests na porta ${env.NEXT_PORT} ---"
                        sh """
                            docker run --rm --network host -i loadimpact/k6 run - < smoke_test.js \
                            -e AUTH_URL="http://localhost:${env.NEXT_PORT}/api/public/login"
                        """
                    }
                }
            }
        }

        stage('Switch Traffic & Cleanup') {
            steps {
                script {
                    echo "--- Sucesso! Removendo versão antiga: ${env.ACTIVE_COLOR} ---"
                    dir('lms-authnusers') {
                        sh "docker compose -p users-${env.ACTIVE_COLOR} down || true"
                    }
                }
            }
        }
    }

    post {
        failure {
            script {
                echo "--- Falha no Deploy. Limpando tentativa de ${env.NEXT_COLOR} ---"
                dir('lms-authnusers') {
                    sh "docker compose -p users-${env.NEXT_COLOR} down || true"
                }
            }
        }
        always { cleanWs() }
    }
}