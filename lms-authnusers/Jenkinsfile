pipeline {
    agent any
    tools { maven 'M3' }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-authnusers'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
        VM_ADDRESS = 'vs578.dei.isep.ipp.pt'
    }

    stages {
        stage('Checkout & Build') {
            steps {
                checkout scm
                dir('lms-authnusers') { sh 'mvn clean package -DskipTests' }
            }
        }

        stage('Build & Push Image') {
            steps {
                dir('lms-authnusers') {
                    script {
                        def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"
                        docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                            def img = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            img.push("${env.BUILD_TAG}")
                            img.push("latest")
                        }
                    }
                }
            }
        }

        stage('Canary Deploy (Weight 0)') {
            steps {
                dir('lms-authnusers') {
                    script {
                        def isGreenActive = sh(script: "docker ps --format '{{.Names}}' | grep users-green || true", returnStdout: true).trim()
                        env.ACTIVE_COLOR = isGreenActive ? "green" : "blue"
                        env.NEXT_COLOR = isGreenActive ? "blue" : "green"
                        env.NEXT_PORT = (env.NEXT_COLOR == "blue") ? "30080" : "30081"

                        echo "--- Subindo ${env.NEXT_COLOR} com PESO 0 (Sem tráfego público) ---"

                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=0"
                        ]) {
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d"

                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' users-${env.NEXT_COLOR}", returnStdout: true).trim()
                                    return (health == "healthy")
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Verify Canary (k6)') {
            steps {
                dir('lms-authnusers') {
                    script {
                        // Teste direto no IP/Porta do container novo (ignora o Traefik)
                        sh "docker run --rm --network lms_network -i loadimpact/k6 run - < verify_strategies.js -e TARGET_URL='http://users-${env.NEXT_COLOR}:8080'"
                    }
                }
            }
        }

        stage('Manual Approval & Traffic Switch') {
            steps {
                input message: "Promover ${env.NEXT_COLOR} para 100% do tráfego?", ok: "Switch Now"
                script {
                    dir('lms-authnusers') {
                        echo "--- Switch: ${env.NEXT_COLOR} (100%) | ${env.ACTIVE_COLOR} (0%) ---"

                        // Ativa o novo
                        withEnv(["DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}", "COLOR=${env.NEXT_COLOR}", "HOST_PORT=${env.NEXT_PORT}", "WEIGHT=100"]) {
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d --no-deps users"
                        }

                        // Desativa tráfego no antigo (opcional, mas recomendado antes do cleanup)
                        withEnv(["DOCKER_IMAGE=${IMAGE_FULL_NAME}:latest", "COLOR=${env.ACTIVE_COLOR}", "HOST_PORT=${env.ACTIVE_COLOR == 'blue' ? '30080' : '30081'}", "WEIGHT=0"]) {
                            sh "docker compose -p users-${env.ACTIVE_COLOR} up -d --no-deps users"
                        }
                    }
                }
            }
        }

        stage('Final Smoke Tests') {
            steps {
                dir('lms-authnusers') {
                    // Teste via Traefik (porta 80) para garantir que o utilizador final está OK
                    sh "docker run --rm --network host -i loadimpact/k6 run - < smoke_test.js -e AUTH_URL='http://localhost/api/users/public/login'"
                }
            }
        }

        stage('Cleanup Old Version') {
            steps {
                sh "docker compose -p users-${env.ACTIVE_COLOR} down || true"
            }
        }
    }

    post {
        failure {
            script {
                echo "Rollback: Removendo tentativa falhada de ${env.NEXT_COLOR}"
                sh "docker compose -p users-${env.NEXT_COLOR} down || true"
            }
        }
    }
}