pipeline {
    agent any

    tools {
        maven 'M3'
    }

    parameters {
        string(name: 'BRANCH', defaultValue: 'main', description: 'Branch a construir')
    }

    environment {
        DOCKER_REGISTRY_CREDENTIALS = 'docker-hub-credentials'
        DOCKER_IMAGE_NAME = 'lms-authnusers'
        DOCKER_NAMESPACE = 'cralos062'
        IMAGE_FULL_NAME = "${DOCKER_NAMESPACE}/${DOCKER_IMAGE_NAME}"
        SSH_CREDENTIALS_ID = 'isep-vm-root'
        VM_ADDRESS = 'vs578.dei.isep.ipp.pt'
    }

    stages {
        stage('Checkout') {
            steps { checkout scm }
        }

        stage('Build & Unit Tests') {
            steps {
                dir('lms-authnusers') { sh 'mvn clean test' }
            }
        }

        stage('Static Analysis & Mutation') {
            parallel {
                stage('SpotBbugs') {
                    steps { dir('lms-authnusers') { sh 'mvn com.github.spotbugs:spotbugs-maven-plugin:4.8.6.2:spotbugs' } }
                }
                stage('Pitest') {
                    steps { dir('lms-authnusers') { sh 'mvn org.pitest:pitest-maven:mutationCoverage' } }
                }
            }
        }

        stage('Package Artifact') {
            steps {
                dir('lms-authnusers') {
                    sh 'mvn package -DskipTests'
                    archiveArtifacts artifacts: 'target/*.jar'
                }
            }
        }

        stage('Build & Push Docker Image') {
            steps {
                dir('lms-authnusers') {
                    script {
                        def shortCommit = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                        env.BUILD_TAG = "${env.BUILD_NUMBER}-${shortCommit}"

                        docker.withRegistry('https://index.docker.io/v1/', DOCKER_REGISTRY_CREDENTIALS) {
                            def customImage = docker.build("${IMAGE_FULL_NAME}:${env.BUILD_TAG}")
                            customImage.push("${env.BUILD_TAG}")
                            customImage.push("latest")
                        }
                    }
                }
            }
        }

        stage('Canary Deploy (Weight 0)') {
            steps {
                dir('lms-authnusers') {
                    script {
                        // Detectar cor ativa
                        def isGreenActive = sh(script: "docker ps --format '{{.Names}}' | grep users-green || true", returnStdout: true).trim()
                        env.ACTIVE_COLOR = isGreenActive ? "green" : "blue"
                        env.NEXT_COLOR = isGreenActive ? "blue" : "green"
                        env.NEXT_PORT = (env.NEXT_COLOR == "blue") ? "30080" : "30081"

                        echo "Ativo: ${env.ACTIVE_COLOR} | Deploying Canary: ${env.NEXT_COLOR} com Peso 0"

                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=0" // Nova versão entra sem tráfego público
                        ]) {
                            sh "docker network create lms_network || true"
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d"

                            echo "--- Health Check da nova versão (${env.NEXT_COLOR}) ---"
                            timeout(time: 5, unit: 'MINUTES') {
                                waitUntil {
                                    def health = sh(script: "docker inspect --format='{{.State.Health.Status}}' users-${env.NEXT_COLOR}", returnStdout: true).trim()
                                    return (health == "healthy")
                                }
                            }
                        }
                    }
                }
            }
        }

        stage('Verify Strategies (k6)') {
            steps {
                dir('lms-authnusers') {
                    script {
                        def containerName = "users-${env.NEXT_COLOR}"
                        echo "--- Validando Canary no container: ${containerName} ---"

                        withEnv([
                            "KILLSWITCH_ACTIVE=true",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=0",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"
                        ]) {
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d --no-deps users"
                            sleep 10
                            // k6 testa diretamente o container via rede interna
                            sh "docker run --rm --network lms_network -i loadimpact/k6 run - < verify_strategies.js -e TARGET_URL='http://${containerName}:8080'"
                        }

                        withEnv([
                            "KILLSWITCH_ACTIVE=false",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=0",
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}"
                        ]) {
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d --no-deps users"
                        }
                    }
                }
            }
        }

        stage('Manual Approval & Switch') {
            steps {
                input message: "Canary ${env.NEXT_COLOR} validado. Deseja promover para 100% do tráfego?", ok: "Promover"

                script {
                    dir('lms-authnusers') {
                        echo "--- Promovendo ${env.NEXT_COLOR} para Peso 100 ---"
                        withEnv([
                            "DOCKER_IMAGE=${IMAGE_FULL_NAME}:${env.BUILD_TAG}",
                            "COLOR=${env.NEXT_COLOR}",
                            "HOST_PORT=${env.NEXT_PORT}",
                            "WEIGHT=100"
                        ]) {
                            sh "docker compose -p users-${env.NEXT_COLOR} up -d --no-deps users"
                        }
                    }
                }
            }
        }

        stage('Cleanup Old Version') {
            steps {
                script {
                    echo "--- Sucesso! Removendo versão antiga: ${env.ACTIVE_COLOR} ---"
                    dir('lms-authnusers') {
                        sh "docker compose -p users-${env.ACTIVE_COLOR} down || true"
                    }
                }
            }
        }
    }

    post {
        failure {
            script {
                echo "--- Falha no Canary. Removendo ${env.NEXT_COLOR} ---"
                dir('lms-authnusers') {
                    sh "docker compose -p users-${env.NEXT_COLOR} down || true"
                }
            }
        }
        always { cleanWs() }
    }
}